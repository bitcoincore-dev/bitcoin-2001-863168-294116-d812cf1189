# Copyright (c) 2024-present The Bitcoin Core developers
# Distributed under the MIT software license, see the accompanying
# file COPYING or https://opensource.org/license/mit/.

find_program(LCOV_EXECUTABLE lcov REQUIRED)
find_program(GENHTML_EXECUTABLE genhtml REQUIRED)

if("@CMAKE_CXX_COMPILER_ID@" STREQUAL "Clang")
  find_program(LLVM_COV_EXECUTABLE llvm-cov REQUIRED)
  set(COV_TOOL "${LLVM_COV_EXECUTABLE} gcov")
else()
  find_program(GCOV_EXECUTABLE gcov REQUIRED)
  set(COV_TOOL "${GCOV_EXECUTABLE}")
endif()

configure_file(
  cmake/cov_tool_wrapper.sh.in ${CMAKE_CURRENT_LIST_DIR}/cov_tool_wrapper.sh
  FILE_PERMISSIONS OWNER_READ OWNER_EXECUTE
                   GROUP_READ GROUP_EXECUTE
                   WORLD_READ
  @ONLY
)

set(LCOV_OPTS)
foreach(setting IN LISTS LCOV_RC)
  list(APPEND LCOV_OPTS --rc ${setting})
endforeach()

set(LCOV_COMMAND ${LCOV_EXECUTABLE} --gcov-tool ${CMAKE_CURRENT_LIST_DIR}/cov_tool_wrapper.sh ${LCOV_OPTS})

set(LCOV_FILTER_PATTERN)
list(APPEND LCOV_FILTER_PATTERN -p "/usr/local/")
list(APPEND LCOV_FILTER_PATTERN -p "/usr/include/")
list(APPEND LCOV_FILTER_PATTERN -p "/usr/lib/")
list(APPEND LCOV_FILTER_PATTERN -p "/usr/lib64/")
list(APPEND LCOV_FILTER_PATTERN -p "src/leveldb/")
list(APPEND LCOV_FILTER_PATTERN -p "src/crc32c/")
list(APPEND LCOV_FILTER_PATTERN -p "src/bench/")
list(APPEND LCOV_FILTER_PATTERN -p "src/crypto/ctaes")
list(APPEND LCOV_FILTER_PATTERN -p "src/minisketch")
list(APPEND LCOV_FILTER_PATTERN -p "src/secp256k1")
list(APPEND LCOV_FILTER_PATTERN -p "depends")

if(JOBS)
  list(APPEND CMAKE_CTEST_COMMAND -j ${JOBS})
endif()

execute_process(COMMAND ${LCOV_COMMAND} --capture --initial --directory ${CMAKE_CURRENT_LIST_DIR}/src --output-file ${CMAKE_CURRENT_LIST_DIR}/baseline.info)
execute_process(COMMAND contrib/filter-lcov.py ${LCOV_FILTER_PATTERN} ${CMAKE_CURRENT_LIST_DIR}/baseline.info ${CMAKE_CURRENT_LIST_DIR}/baseline_filtered.info)
execute_process(COMMAND ${LCOV_COMMAND} --add-tracefile ${CMAKE_CURRENT_LIST_DIR}/baseline_filtered.info --output-file ${CMAKE_CURRENT_LIST_DIR}/baseline_filtered.info)
execute_process(COMMAND ${CMAKE_CTEST_COMMAND} --build-config Coverage --test-dir ${CMAKE_CURRENT_LIST_DIR})
execute_process(COMMAND ${LCOV_COMMAND} --capture --directory ${CMAKE_CURRENT_LIST_DIR}/src --test-name test_bitcoin --output-file ${CMAKE_CURRENT_LIST_DIR}/test_bitcoin.info)
execute_process(COMMAND ${LCOV_COMMAND} --zerocounters --directory ${CMAKE_CURRENT_LIST_DIR}/src)
execute_process(COMMAND contrib/filter-lcov.py ${LCOV_FILTER_PATTERN} ${CMAKE_CURRENT_LIST_DIR}/test_bitcoin.info ${CMAKE_CURRENT_LIST_DIR}/test_bitcoin_filtered.info)
execute_process(COMMAND ${LCOV_COMMAND} --add-tracefile ${CMAKE_CURRENT_LIST_DIR}/test_bitcoin_filtered.info --output-file ${CMAKE_CURRENT_LIST_DIR}/test_bitcoin_filtered.info)
execute_process(COMMAND ${GENHTML_EXECUTABLE} ${LCOV_OPTS} --show-details ${CMAKE_CURRENT_LIST_DIR}/test_bitcoin_filtered.info --output-directory ${CMAKE_CURRENT_LIST_DIR}/test_bitcoin.coverage)
